<program> ===> <otherFunctions> <mainFunction>
<mainFunction> ===> TK_MAIN <stmts> TK_END
<otherFunctions> ===> <function> <otherFunctions>
<otherFunctions> ===> ePSILON
<function> ===> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> ===> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> ePSILON
<parameter_list> ===> <dataType> TK_ID <remaining_list>
<dataType> ===> <primitiveDatatype>
<dataType> ===> <constructedDatatype>
<primitiveDatatype> ===> TK_INT
<primitiveDatatype> ===> TK_REAL
<constructedDatatype> ===> <A> TK_RUID
<constructedDatatype> ===> TK_RUID
<remaining_list> ===> TK_COMMA <parameter_list>
<remaining_list> ===> ePSILON
<stmts> ===> <typeDefinitions> <declarations> <otherStmts> <returnStmt>
<typeDefinitions> ===> <typeDefinition> <typeDefinitions>
<typeDefinitions> ===> <definetypestmt> <typeDefinitions>
<typeDefinitions> ===> ePSILON
<typeDefinition> ===> TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD
<typeDefinition> ===> TK_UNION TK_RUID <fieldDefinitions> TK_ENDUNION
<fieldDefinitions> ===> <fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition> ===> TK_TYPE <datatype> TK_COLON TK_FIELDID TK_SEM
<moreFields> ===> <fieldDefinition> <moreFields>
<moreFields> ===> ePSILON
<declarations> ===> <declaration> <declarations> | ePSILON
<declaration> ===> TK_TYPE <dataType> TK_COLON TK_ID <optional_global> TK_SEM
<optional_global> ===> TK_COLON TK_GLOBAL | ePSILON
<otherStmts> ===> <stmt> <otherStmts>
<otherStmts> ===> ePSILON
<stmt> ===> <assignmentStmt>
<stmt> ===> <iterativeStmt> | <conditionalStmt> | <ioStmt> | <funCallStmt>
<assignmentStmt> ===> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId> ===> TK_ID <recID>
<recID> ===> TK_DOT TK_FIELDID <recID>
<recID> ===> ePSILON
<funCallStmt> ===> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ===> TK_SQL <idList> TK_SQR TK_ASSIGNOP
<outputParameters> ===> ePSILON
<inputParameters> ===> TK_SQL <idList> TK_SQR
<iterativeStmt> ===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE
<conditionalStmt> ===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <new3> TK_ENDIF 
<new3> ===> TK_ELSE <stmt> <otherStmts>
<new3> ===> ePSILON
<ioStmt> ===> TK_READ TK_OP <var> TK_CL TK_SEM
<ioStmt> ===> TK_WRITE TK_OP <var> TK_CL TK_SEM
<arithmeticExpression> ===> <term> <new5>
<new5> ===> <operator> <term> <new5>
<new5> ===> ePSILON
<term> ===> <factor> <new6>
<new6> ===> <op_H> <factor> <new6>
<new6> ===> ePSILON
<factor> ===> <var>
<factor> ===> TK_OP <arithmeticExpression> TK_CL
<operator> ===> TK_PLUS
<operator> ===> TK_MINUS
<op_H> ===> TK_MUL
<op_H> ===> TK_DIV
<booleanExpression> ===> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL
<booleanExpression> ===> <var> <relationalOp> <var>
<booleanExpression> ===> TK_NOT TK_OP <booleanExpression> TK_CL 
<var> ===> <singleOrRecId>
<var> ===> TK_NUM
<var> ===> TK_RNUM 
<logicalOp> ===> TK_AND
<logicalOp> ===> TK_OR
<relationalOp> ===> TK_LT
<relationalOp> ===> TK_LE
<relationalOp> ===> TK_EQ |TK_GT
<relationalOp> ===> TK_GE
<relationalOp> ===> TK_NE
<returnStmt> ===> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> ===> TK_SQL <idList> TK_SQR
<optionalReturn> ===> ePSILON
<idList> ===> TK_ID <more_ids>
<more_ids> ===> TK_COMMA TK_ID <more_ids>
<more_ids> ===> ePSILON
<definetypestmt> ===> TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID 
<A> ===> TK_RECORD
<A> ===> TK_UNION

